import os.path

# Build tools
asmBuilder = Builder(action = 'asm.py -o $TARGET $SOURCE', suffix=".o", src_suffix=[".s", ".asm"], single_source=True, src_builder="Natrix")
natrixBuilder = Builder(action = 'natrix.py --cpp-arg=-Ilib -o $TARGET $SOURCE', suffix=".s", src_suffix=".na", single_source=True, source_scanner=CScanner)
def linkGenerator(source, target, env, for_signature):
    return f"link.py -m {target[1]} -o {target[0]} {' '.join(str(s) for s in source)}"

def linkEmitter(target, source, env):
    t = target[0]
    base,ext = os.path.splitext(str(t))
    target.append(f"{base}.map")
    return target, source

linkBuilder = Builder(generator=linkGenerator, suffix=".bin", src_suffix=".o", src_builder="Asm", emitter=linkEmitter)
env = Environment(BUILDERS = {'Asm' : asmBuilder, 'Natrix': natrixBuilder, 'Bin': linkBuilder})
env.PrependENVPath("PATH", "../tools")
env.PrependENVPath("PATH", "../tools/natrix")
env["NATRIX_DIR"] = "../tools/natrix"
env["CPPPATH"] = "lib" # needed for scanner


# Libraries
bcdf = Split('''
    lib/bcdf.asm lib/bcdf_addsub.asm lib/bcdf_mul.asm lib/bcdf_div.asm lib/bcdf_print.asm
    ''')
natrix = ['$NATRIX_DIR/runtime/natrix_runtime.asm']

ps2 = Split('lib/ps2.na lib/ps2keyboard.na')

vga = Split('''
    lib/vga.asm
    lib/vga_console.na
    ''')

keyboard = ['lib/keyboard.asm']

lib = bcdf + vga + ps2 + keyboard + Split('''
    lib/string.na
    lib/entropy.na
    lib/random.na
    ''')

# Apps
maze = Split('maze/main.na maze/game.na maze/maze.na')
life = Split('life/quasipixel.na life/life.na life/life_next.asm life/qp_render.asm')
calc = Split('calc/calc.asm')

# Targets
env.Bin('main', Split('''
    main.na
    test/trap.asm
    vga_demo.asm
    matrix.na''') + lib + natrix + maze + life + calc)

env.Bin('alutest', 'test/alutest.asm')

env.Bin('memcheck', 'memcheck/memcheck.asm')

env.Bin('lotest', Split('memcheck/lotest.na') + natrix + lib)

env.Bin('vgatest', Split('test/vgatest.na') + natrix + lib)

env.Bin('ps2test', Split('test/ps2test.na') + natrix + lib)

env.Bin('ps2hightest', Split('test/ps2hightest.na') + natrix + lib)

env.Bin('cardtest', Split('test/cardtest.na lib/card.na lib/fat.na') + natrix + lib)
